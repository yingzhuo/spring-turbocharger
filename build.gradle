plugins {
	id 'buildlogic.root-project-conventions'
}

defaultTasks 'clean'

description = 'Another enhancement library of SpringBoot & SpringFramework.'

['versions.xml', 'gradle.xml']
	.stream()
	.forEach { filePath ->
		var file = file(filePath)
		if (file.exists() && file.isFile()) {
			var props = new Properties()

			if (filePath.endsWithIgnoreCase('.properties')) {
				file.withDataInputStream { stream ->
					props.load(stream)
				}
			}

			if (filePath.endsWithIgnoreCase('.xml')) {
				file.withDataInputStream { stream ->
					props.loadFromXML(stream)
				}
			}

			props.forEach(ext::set)
		}
	}

ext {
	// SpringBoot BOM 版本微调
	// https://docs.spring.io/spring-boot/appendix/dependency-versions/coordinates.html
	// https://docs.spring.io/spring-boot/appendix/dependency-versions/properties.html
}

subprojects {
	configurations.configureEach {
		resolutionStrategy {
			cacheChangingModulesFor 72, 'hours'
			cacheDynamicVersionsFor 72, 'hours'
		}
	}
}

allprojects {
	group = project.springTurbochargerGroup
	version = project.springTurbochargerVersion
}
