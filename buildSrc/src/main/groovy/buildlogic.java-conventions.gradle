import org.apache.tools.ant.filters.ReplaceTokens

plugins {
	id 'java'
	id 'java-library'
	id 'io.spring.dependency-management'
}

java {
	final var javaVersionInt = javaVersion as Integer
	sourceCompatibility = JavaVersion.toVersion(javaVersionInt)
	targetCompatibility = JavaVersion.toVersion(javaVersionInt)

	toolchain {
		languageVersion = JavaLanguageVersion.of(javaVersionInt)
		vendor = JvmVendorSpec.ORACLE
		implementation = JvmImplementation.VENDOR_SPECIFIC
	}

	withSourcesJar()
	withJavadocJar()
}

compileJava {
	options.compilerArgs += [
		'-parameters',
		'-Xlint:unchecked',
		'-Xlint:cast',
		'-Xlint:rawtypes',
		'-Xlint:overloads',
		'-Xlint:divzero',
		'-Xlint:finally',
		'-Xlint:static',
		// '-Werror',
	]
}

jar {
	manifest {
		final var attr = [
			'Module-Name'           : project.name,
			'Implementation-Version': project.version,
			'Build-Jdk-Spec'        : javaVersion,
			'Created-By'            : "Gradle ${gradle.gradleVersion}"
		]
		attributes(attr)
	}
}

sourcesJar {
}

javadoc {
	failOnError = false

	options {
		locale = 'en_US'
		encoding = 'UTF-8'
		addBooleanOption("html5", true)
		addBooleanOption("Xdoclint:none", true)
	}
}

final var replaceTokenMap = [
	'APP_GROUP'  : project.group,
	'APP_NAME'   : project.name,
	'APP_VERSION': project.version
]

processResources {
	exclude('**/.DS_Store', '**/.gitkeep')

	from(rootDir) {
		include('LICENSE.txt', 'NOTICE.txt')
		into 'META-INF'
	}

	from('src/main/resources') {
		include('**/*.yaml', '**/*.txt')
		filter(ReplaceTokens, 'tokens': replaceTokenMap)
		filteringCharset = 'UTF-8'
	}

	duplicatesStrategy = DuplicatesStrategy.INCLUDE
}

processTestResources {
	exclude('**/.DS_Store', '**/.gitkeep')

	from('src/test/resources') {
		include('**/*.yaml', '**/*.txt')
		filter(ReplaceTokens, 'tokens': replaceTokenMap)
		filteringCharset = 'UTF-8'
	}

	duplicatesStrategy = DuplicatesStrategy.INCLUDE
}

test {
	useJUnitPlatform()
}
