/*
 *
 * Copyright 2022-present the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 *
 */
plugins {
	id 'org.springframework.boot'
	id 'com.gorylenko.gradle-git-properties'
}

tasks.named('jar') {
	enabled = false
}

tasks.named('bootJar') {
	manifest {
		attributes([
			'Main-Class': 'org.springframework.boot.loader.launch.PropertiesLauncher',
			'Built-By'  : "Gradle ${gradle.gradleVersion}"
		])
	}

	archiveFileName = "${project.name}-${project.version}.jar"

	includeTools = true

	layered {
		enabled = true

		application {
			intoLayer('spring-boot-loader') {
				include 'org/springframework/boot/loader/**'
			}
			intoLayer('application')
		}

		dependencies {
			intoLayer('application') {
				includeProjectDependencies()
			}

			intoLayer('snapshot-dependencies') {
				include '*:*:*SNAPSHOT'
			}

			intoLayer('dependencies')
		}

		layerOrder = ['dependencies',
									'spring-boot-loader',
									'snapshot-dependencies',
									'application']
	}

	excludes = [
		'**/.gitkeep',
		'**/.DS_Store',
		'**/netty-*-macos*.jar',
		'**/netty-*-osx*.jar'
	]
}

tasks.named('bootBuildImage') {
	enabled = false
}

springBoot {
	buildInfo {
		enabled = true
	}
}

gitProperties {
	dotGitDirectory = rootProject.layout.projectDirectory.dir(".git/")
	gitPropertiesName = 'git.properties'
	keys = ['git.branch', 'git.commit.id', 'git.commit.id.abbrev', 'git.commit.time', 'git.dirty']
	dateFormat = "$datetimeFormat"
	dateFormatTimeZone = "$timezone"
	failOnNoGitDirectory = true
}
