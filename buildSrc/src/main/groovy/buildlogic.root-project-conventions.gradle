plugins {
	id 'base'
	id 'buildlogic.binary-plugin.license'
}

tasks.named('wrapper') { task ->
	// 国内镜像两个比较好的选择
	// (1) 腾讯云: https://mirrors.cloud.tencent.com/gradle/gradle-$gradleWrapperVersion-bin.zip
	// (2) 华为云: https://mirrors.huaweicloud.com/gradle/gradle-$gradleWrapperVersion-bin.zip
	distributionUrl = "https://mirrors.cloud.tencent.com/gradle/gradle-$gradleWrapperVersion-bin.zip"
}

tasks.register('removeWrapper', Delete) { task ->
	group = 'build setup'
	description = 'Remove Gradle wrapper files.'

	followSymlinks = true
	delete fileTree(rootDir) {
		include 'gradlew'
		include 'gradlew.bat'
		include 'gradle/wrapper/'
	}
}

tasks.register('pushAllCodes', Exec) { task ->
	group = 'git'
	description = 'Commit and push codes to gitee/github'

	workingDir(rootDir)
	commandLine('bash', "$rootDir/buildSrc/config/shell/push.sh")
}

tasks.named('clean') { task ->
	mustRunAfter(
		SharedFunctions.getLeafProjectNames(rootProject).collect { "${it}:clean" }
	)

	delete fileTree(rootDir) {
		include '**/.DS_Store'
		include '**/*.log'
	}
}

addLicenseHeader {
	javaHeader =
		'''
		|/*
		| *
		| * Copyright 2022-present the original author or authors.
		| *
		| * Licensed under the Apache License, Version 2.0 (the "License");
		| * you may not use this file except in compliance with the License.
		| * You may obtain a copy of the License at
		| *
		| *      https://www.apache.org/licenses/LICENSE-2.0
		| *
		| * Unless required by applicable law or agreed to in writing, software
		| * distributed under the License is distributed on an "AS IS" BASIS,
		| * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
		| * See the License for the specific language governing permissions and
		| * limitations under the License.
		| *
		| */
		'''
			.stripMargin('|')
			.trim()
}
