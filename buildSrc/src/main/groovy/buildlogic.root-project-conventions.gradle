plugins {
	id 'base'
	id 'buildlogic.binary-plugin.license'
}

defaultTasks 'classes'

tasks.named('wrapper') {
	distributionUrl = gradleWrapperDistributionUrl
}

tasks.register('pushToVcs', Exec) {
	group = 'vcs'
	description = 'Commit and push codes to gitee/github.'

	workingDir(rootDir)
	commandLine('bash', "$rootDir/buildSrc/config/vcs/push.sh")
}

tasks.named('clean') {
	mustRunAfter(SharedFunctions.getLeafProjectNames(rootProject).collect { "${it}:clean" })

	delete fileTree(rootDir) {
		include '**/.DS_Store'
		include '**/*.log'
	}
}

addLicenseHeader {
	javaHeader =
		'''
		|/*
		| *
		| * Copyright 2022-present the original author or authors.
		| *
		| * Licensed under the Apache License, Version 2.0 (the "License");
		| * you may not use this file except in compliance with the License.
		| * You may obtain a copy of the License at
		| *
		| *      https://www.apache.org/licenses/LICENSE-2.0
		| *
		| * Unless required by applicable law or agreed to in writing, software
		| * distributed under the License is distributed on an "AS IS" BASIS,
		| * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
		| * See the License for the specific language governing permissions and
		| * limitations under the License.
		| *
		| */
		'''
			.stripMargin('|')
			.trim()
}
