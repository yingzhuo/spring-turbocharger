plugins {
	id 'java-platform'
	id 'maven-publish'
	id 'signing'
	id 'io.gitee.pkmer.pkmerboot-central-publisher'
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.javaPlatform

			groupId = project.group
			artifactId = project.name
			version = project.version

			versionMapping {
				usage('java-runtime') {
					fromResolutionResult()
				}
			}

			pom {
				name = project.name
				description = 'BOM of Spring Turbocharger'
				url = 'https://github.com/yingzhuo/spring-turbocharger'
				inceptionYear = '2025'

				licenses {
					license {
						name = 'Apache License, Version 2.0'
						url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
					}
				}

				developers {
					developer {
						id = 'yingzhuo'
						name = '应卓'
						email = 'yingzhor@gmail.com'
						roles = ['author']
						url = 'https://github.com/yingzhuo'
					}
				}

				scm {
					url = 'git@github.com:yingzhuo/spring-turbocharger.git'
					connection = 'scm:git:git@github.com:yingzhuo/spring-turbocharger.git'
					developerConnection = 'scm:git:git@github.com:yingzhuo/spring-turbocharger.git'
				}

				issueManagement {
					system = 'GitHub Issues'
					url = 'https://github.com/yingzhuo/spring-turbocharger/issues'
				}
			}
		}
	}

	repositories {
		maven {
			name = "Local"
			url = layout.buildDirectory.dir('repos/bundles')
		}
	}
}

signing {
	useGpgCmd()
	sign publishing.publications.mavenJava
}

tasks.withType(GenerateModuleMetadata).configureEach {
	enabled = false
}

pkmerBoot {
	sonatypeMavenCentral {
		stagingRepository = layout.buildDirectory.dir("repos/bundles")
		username = getProperty('sonatypeUsername')
		password = getProperty('sonatypePassword')
	}
}

tasks.named('publishToMavenCentralPortal') { task ->
	doFirst {
		if ("$springTurbochargerVersion".endsWith('-SNAPSHOT')) {
			throw new GradleException('Not able to publish SNAPSHOT items')
		}
	}
}
