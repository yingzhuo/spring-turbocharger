/*
 *
 * Copyright 2022-present the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 *
 */
plugins {
	id 'groovy-gradle-plugin'
}

['../gradle.properties', 'gradle.properties'].each {
	try (var reader = file(it).newReader('utf-8')) {
		final var props = new Properties()
		props.load(reader)
		props.forEach(ext::set)
	}
}

dependencies {
	implementation "org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion"
	implementation "com.gorylenko.gradle-git-properties:gradle-git-properties:$gitPluginVersion"
	implementation "io.gitee.pkmer.pkmerboot-central-publisher:io.gitee.pkmer.pkmerboot-central-publisher.gradle.plugin:$mavenCentralPublisherPluginVersion"
}

gradlePlugin {
	plugins {
		create('LicenseAddingPlugin') {
			id = 'buildlogic.binary-plugin.license'
			implementationClass = 'buildlogic.gradle.plugin.LicensePlugin'
		}
	}
}
