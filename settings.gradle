/*
 *
 * Copyright 2022-present the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 *
 */
pluginManagement {
	repositories {
		mavenLocal()
		gradlePluginPortal()
		mavenCentral()

		if (providers.gradleProperty('useSpringMavenRepositoryMirrors').getOrElse('true').toBoolean()) {
			maven { url = 'https://repo.spring.io/release' }
			maven { url = 'https://repo.spring.io/milestone' }
			maven { url = 'https://repo.spring.io/snapshot' }
		}
	}
}

plugins {
	id 'io.spring.dependency-management' version "$dependencyManagementPluginVersion" apply false
	id 'io.gitee.pkmer.pkmerboot-central-publisher' version "$mavenCentralPublisherPluginVersion" apply false
	id 'com.gorylenko.gradle-git-properties' version "$gitPluginVersion" apply false
}

dependencyResolutionManagement {
	repositories {
		mavenLocal()

		if (providers.gradleProperty('useChinaMavenRepositoryMirrors').getOrElse('true').toBoolean()) {
			maven { url = 'https://maven.aliyun.com/repository/public/' }
			maven { url = 'https://mirrors.cloud.tencent.com/nexus/repository/maven-public/' }
			maven { url = 'https://repo.huaweicloud.com/repository/maven/' }
			maven { url = 'https://maven.jdcloud.com/repos/content/groups/public/' }
			maven { url = 'https://mirrors.163.com/maven/repository/maven-public/' }
		}

		mavenCentral()
		gradlePluginPortal()

		if (providers.gradleProperty('useSpringMavenRepositoryMirrors').getOrElse('true').toBoolean()) {
			maven { url = 'https://repo.spring.io/release' }
			maven { url = 'https://repo.spring.io/milestone' }
			maven { url = 'https://repo.spring.io/snapshot' }
		}

		google()
	}

	repositoriesMode = RepositoriesMode.FAIL_ON_PROJECT_REPOS
}

rootProject.name = 'spring-turbocharger'

['projects-main', 'projects-example'].each {
	file(it).eachDirMatch(~/.*/) { projectDir ->
		include "$it:${projectDir.name}"
	}
}
