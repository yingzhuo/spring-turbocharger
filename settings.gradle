pluginManagement {
	repositories {
		mavenLocal()
		maven { url = 'https://maven.aliyun.com/repository/gradle-plugin' }
		maven { url = 'https://maven.aliyun.com/repository/spring-plugin' }
		gradlePluginPortal()
		mavenCentral()
	}
}

plugins {
	id 'io.spring.dependency-management' version "$dependencyManagementPluginVersion" apply false
	id 'io.gitee.pkmer.pkmerboot-central-publisher' version "$mavenCentralPublisherPluginVersion" apply false
}

dependencyResolutionManagement {
	evaluate(file('buildSrc/config/maven/RepositoriesSupport.groovy')).apply(this)
	repositoriesMode = RepositoriesMode.FAIL_ON_PROJECT_REPOS
}

rootProject.name = 'spring-turbocharger'

['projects-main', 'projects-example'].each {
	file(it).eachDirMatch(~/.*/) { projectDir ->
		include "$it:${projectDir.name}"
	}
}
