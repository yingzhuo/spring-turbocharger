pluginManagement {
	repositories {
		mavenLocal()
		gradlePluginPortal()
		mavenCentral()
		maven { name = 'Spring (Release)'; url = 'https://repo.spring.io/release' }
		maven { name = 'Spring (Milestone)'; url = 'https://repo.spring.io/milestone' }
		maven { name = 'Spring (Snapshot)'; url = 'https://repo.spring.io/snapshot' }
	}
}

plugins {
	id 'io.spring.dependency-management' version "$dependencyManagementPluginVersion" apply false
	id 'io.gitee.pkmer.pkmerboot-central-publisher' version "$mavenCentralPublisherPluginVersion" apply false
	id 'dev.yumi.gradle.licenser' version "$licensePluginVersion" apply false
}

dependencyResolutionManagement {
	repositories {
		mavenLocal()
		maven { name = '阿里云'; url = 'https://maven.aliyun.com/repository/public/' }
		maven { name = '腾讯云'; url = 'https://mirrors.cloud.tencent.com/nexus/repository/maven-public/' }
		maven { name = '华为云'; url = 'https://repo.huaweicloud.com/repository/maven/' }
		maven { name = '京东云'; url = 'https://maven.jdcloud.com/repos/content/groups/public/' }
		maven { name = '网易云'; url = 'https://mirrors.163.com/maven/repository/maven-public/' }
		mavenCentral()
		gradlePluginPortal()
		maven { name = 'Spring (Release)'; url = 'https://repo.spring.io/release' }
		maven { name = 'Spring (Milestone)'; url = 'https://repo.spring.io/milestone' }
		maven { name = 'Spring (Snapshot)'; url = 'https://repo.spring.io/snapshot' }
	}

	repositoriesMode = RepositoriesMode.FAIL_ON_PROJECT_REPOS
}

rootProject.name = 'spring-turbocharger'

['projects-main', 'projects-example'].each {
	file(it).eachDir { projectDir ->
		if (projectDir.directory && new File(projectDir, 'build.gradle').exists()) {
			include "$it:${projectDir.name}"
		}
	}
}
